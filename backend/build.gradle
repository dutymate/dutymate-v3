plugins {
    id 'java'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.4'
    id "org.sonarqube" version '6.0.1.5171'
}

group = 'net.dutymate'
version = '2.4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    // Core Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Database & Persistence
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.2'

    // Security & Auth
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'org.mindrot:jbcrypt:0.4'

    // Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // AWS SDK
    implementation 'software.amazon.awssdk:s3:2.29.52'

    // XSS 방지
    implementation 'org.jsoup:jsoup:1.20.1'

    // 개발 편의성
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

checkstyle {
    maxWarnings = 0
    configFile = file('config/checkstyle/naver-checkstyle-rules.xml')
    configProperties = ['checkstyle.suppressions.file': 'config/checkstyle/naver-checkstyle-suppressions.xml']
    toolVersion = '10.19.0'
}

jib {
    from {
        image = 'amazoncorretto:21-alpine-jdk'
    }
    to {
        tags = ['latest', "${project.version}".toString()]
        credHelper = 'ecr-login'
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        jvmFlags = ['-XX:+UseContainerSupport', '-Xmx768m', '-Dserver.port=8080', '-Duser.timezone=Asia/Seoul', '-Dfile.encoding=UTF-8']
        ports = ['8080']
    }
}
